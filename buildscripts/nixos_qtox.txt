{ pkgs ? import <nixpkgs> { } }:
with pkgs;
let
  libtoxcore' = libtoxcore.overrideAttrs ({ ... }: {
    src = fetchurl {
      url =
        "https://github.com/TokTok/c-toxcore/releases/download/v0.2.18/c-toxcore-0.2.18.tar.gz";
      sha256 = "sha256-8pQFN5mIY1k+KLxqa19W8JZ19s2KKDJre8MbSDbAiUI=";
    };
    patches = [
      (fetchpatch {
        url = "https://raw.githubusercontent.com/Zoxcore/qTox/zoxcore/push_notification/buildscripts/patches/msgv3_addon.patch";
        sha256 = "sha256-+K3Bpdnxzf0a35mzOnVJjGi5nutpgi6kHRnmz7AZAC4=";
      })
      (fetchpatch {
        url = "https://raw.githubusercontent.com/Zoxcore/qTox/zoxcore/push_notification/buildscripts/patches/tc___capabilites.patch";
        sha256 = "sha256-fEgSxRFY8MWGq7eq86sCU/WhwFVC+XY+tuc+k9ZeecQ=";
      })
    ];
  });

  toxext = stdenv.mkDerivation rec {
    pname = "toxext";
    version = "0.0.3";
    src = fetchFromGitHub {
      owner = pname;
      repo = pname;
      rev = "v${version}";
      hash = "sha256-I0Ay3XNf0sxDoPFBH8dx1ywzj96Vnkqzlu1xXsxmI1M=";
    };
    nativeBuildInputs = [ cmake pkg-config ];
    buildInputs = [ libtoxcore ];
  };

  toxextMessages = stdenv.mkDerivation rec {
    pname = "tox_extension_messages";
    version = "0.0.3";
    src = fetchFromGitHub {
      owner = "toxext";
      repo = pname;
      rev = "v${version}";
      hash = "sha256-qtjtEsvNcCArYW/Zyr1c0f4du7r/WJKNR96q7XLxeoA=";
    };
    nativeBuildInputs = [ cmake pkg-config ];
    buildInputs = [ libtoxcore toxext ];
  };

in pkgs.qtox.overrideAttrs ({ buildInputs, ... }: {
  version = "push_notification-";
  src =
    builtins.fetchGit "https://github.com/Zoxcore/qTox?ref=push_notification";
  buildInputs = buildInputs ++ [ curl libtoxcore' toxext toxextMessages ];
})
